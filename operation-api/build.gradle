jar {
    enabled = true  // ✨ 변경: false → true
    archiveClassifier = ''  // ✨ 추가
}

bootJar {
    enabled = true
//    mainClassName = 'org.sopt.makers.operation.OperationApplication'
}

String jarName = "operation.jar"
tasks.named("bootJar") {
    bootJar.getArchiveFileName().set(jarName)

    bootJar.doLast(task -> {
        copy(copySpec -> {
            copySpec.from("build/libs/" + jarName)
            copySpec.into("../build/libs")
        })
    })
}


// ✨ Lambda ZIP 빌드 태스크 추가
task lambdaJar(type: Zip) {
    dependsOn bootJar
    archiveBaseName = 'operation'
    archiveClassifier = 'lambda'
    destinationDirectory = file("${buildDir}/distributions")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 = true  // 대용량 ZIP 파일 지원

    into('lib') {
        from(jar)
        from(configurations.runtimeClasspath) {
            // Lambda에서 불필요한 파일 제외
            exclude "org/apache/tomcat/embed/**"
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
            exclude "META-INF/MANIFEST.MF"
            exclude "**/module-info.class"
        }
    }
}

def profile = project.findProperty("profile") ?: "test"
println("Build Profile: $profile")

tasks.register("processProfileYaml", Copy) {
    from("src/main/resources/application-${profile}.yaml")
    into("build/resources/main") // 빌드 시 사용할 리소스 경로
    rename { "application.yaml" } // 모든 프로파일 파일을 application.yaml로 변경
}

// processResources 작업 후에 실행되도록 의존성 추가
tasks.named("processResources") {
    dependsOn("processProfileYaml")
}
dependencies {
    // module
    implementation project(path: ':operation-auth')
    implementation project(path: ':operation-common')
    implementation project(path: ':operation-domain')
    implementation project(path: ':operation-external')



    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // jwt builder library
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.2"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.2"

    testImplementation 'com.h2database:h2'
}
